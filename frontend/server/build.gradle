plugins {
    id "com.google.protobuf" version "0.8.10"
    id "java"
}

dependencies {
    compile "io.netty:netty-all:${netty_version}"
    compile project(":modelarchive")
    compile "commons-cli:commons-cli:${commons_cli_version}"
    compile "software.amazon.ai:mms-plugins-sdk:${mms_server_sdk_version}"
    compile "com.google.protobuf:protobuf-java:3.13.0"
    testCompile "org.testng:testng:${testng_version}"
}

apply from: file("${project.rootProject.projectDir}/tools/gradle/launcher.gradle")

jar {
    manifest {
        attributes 'Main-Class': 'com.amazonaws.ml.mms.ModelServer'
    }
    includeEmptyDirs = false
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }

    exclude "META-INF/maven/**"
    exclude "META-INF/INDEX.LIST"
    exclude "META-INF/MANIFEST*"
    exclude "META-INF//LICENSE*"
    exclude "META-INF//NOTICE*"
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.13.0'
    }
}

test.doFirst {
    systemProperty "mmsConfigFile", 'src/test/resources/config.properties'
    systemProperty "METRICS_LOCATION","build/logs"
    systemProperty "LOG_LOCATION","build/logs"
}
